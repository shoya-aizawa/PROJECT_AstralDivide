chcp 65001 >nul
@echo off & setlocal enabledelayedexpansion
for /f %%a in ('cmd /k prompt $e^<nul') do (set "esc=%%a")
mode 80,25


:: --- Config/Cache の保存先を決める（PROJECT_ROOT/CFG_DIR が無くても自力で導出） ---
if not defined PROJECT_ROOT for %%I in ("%~dp0\..\..\..") do set "PROJECT_ROOT=%%~fI"
if defined CFG_DIR (
    set "_cfg=%CFG_DIR%"
) else (
    set "_cfg=%PROJECT_ROOT%\Config"
)
set "_screen_dir=%_cfg%\Cache\Screen\%COMPUTERNAME%"
set "_screen_file=%_screen_dir%\screen_config.env"
if not exist "%_screen_dir%" md "%_screen_dir%" 2>nul

echo %esc%[2J%esc%[1;1H
echo %esc%[93m   ===========================================   %esc%[0m
echo %esc%[93m    Screen Environment Detection System ver.1    %esc%[0m
echo %esc%[93m   ===========================================   %esc%[0m
echo.
%tools_dir%\cmdwiz.exe delay 10

:: 0. Full-screen mode settings
echo %esc%[96m  [0] Enabling fullscreen mode...%esc%[0m
%tools_dir%\cmdwiz.exe fullscreen 1
set "check_fullscreen=%errorlevel%"
echo %esc%[92m   [OK] Fullscreen mode enabled%esc%[0m
%tools_dir%\cmdwiz.exe delay 10

:: 1. Get screen resolution
echo %esc%[96m  [1] Detecting screen resolution... %esc%[0m
:: Get screen resolution with PowerShell
for /f "usebackq tokens=*" %%a in (`powershell -NoProfile -Command "Add-Type -AssemblyName System.Windows.Forms; $s=[System.Windows.Forms.Screen]::PrimaryScreen.Bounds; Write-Output $($s.Width); Write-Output $($s.Height)"`) do (
    if not defined screen_width (
        set screen_width=%%a
    ) else (
        set screen_height=%%a
    )
)
echo %esc%[92m   [OK] Screen resolution: %screen_width% x %screen_height%%esc%[0m
%tools_dir%\cmdwiz.exe delay 10

:: 2. Get the console window size
echo %esc%[96m  [2] Detecting console window size...%esc%[0m
for /f "tokens=*" %%a in ('powershell -Command "$host.UI.RawUI.WindowSize.Width"') do set console_width=%%a
for /f "tokens=*" %%a in ('powershell -Command "$host.UI.RawUI.WindowSize.Height"') do set console_height=%%a
echo %esc%[92m   [OK] Console Size: %console_width% x %console_height%%esc%[0m
%tools_dir%\cmdwiz.exe delay 10

:: 3. Font Size Estimation
echo %esc%[96m  [3] Estimating text size...%esc%[0m
set /a char_width_est=!screen_width!/!console_width!
set /a char_height_est=!screen_height!/!console_height!
echo %esc%[92m   [OK] Estimated text size: %char_width_est% x %char_height_est% ピクセル%esc%[0m
%tools_dir%\cmdwiz.exe delay 10

:: 4. Generate the environment file
echo %esc%[96m  [4] Generating configuration files...%esc%[0m
call :Generate_Environment_Config
echo %esc%[92m   [OK] Environment setting file generation completed%esc%[0m
%tools_dir%\cmdwiz.exe delay 10

:: 5. Compatibility Testing
echo %esc%[96m  [5] Running a display compatibility test%esc%[0m
call :Run_Compatibility_Test
echo %esc%[19;1H%esc%[92m   [OK] Compatibility test completed%esc%[0m
%tools_dir%\cmdwiz.exe delay 10

:: 6. Complete.
%tools_dir%\cmdwiz.exe fullscreen 0
echo %esc%[2J%esc%[1;1H
echo.
echo %esc%[93m   ===========================================   %esc%[0m
echo %esc%[93m               Detection complete.               %esc%[0m
echo %esc%[93m   ===========================================   %esc%[0m
echo.
timeout /t 1 >nul
exit /b 10690000











:Generate_Environment_Config
    rem 既存の %src_env_dir% への書き出しをやめ、_screen_file へ集約
    > "%_screen_file%" (
        echo rem auto-generated by ScreenEnvironmentDetection.bat
        echo rem date: %date% %time%
        echo.
        echo rem Screen resolution [px]
        echo set SCREEN_WIDTH=%screen_width%
        echo set SCREEN_HEIGHT=%screen_height%
        echo.
        echo rem Console size [chars]
        echo set CONSOLE_WIDTH=%console_width%
        echo set CONSOLE_HEIGHT=%console_height%
        echo.
        echo rem Estimated font size [px per char]
        echo set CHAR_WIDTH_EST=%char_width_est%
        echo set CHAR_HEIGHT_EST=%char_height_est%
        echo.
        echo rem Classification
        if %screen_width% LEQ 1366 (
            echo set SCREEN_CLASS=SMALL
            echo set UI_SCALE=0.8
        ) else if %screen_width% LEQ 1920 (
            echo set SCREEN_CLASS=MEDIUM
            echo set UI_SCALE=1.0
        ) else (
            echo set SCREEN_CLASS=LARGE
            echo set UI_SCALE=1.2
        )
        echo.
        echo rem Hints
        echo set COMPUTER=%COMPUTERNAME%
        echo set PROBED_AT=%date% %time%
    )
    exit /b 0


:Run_Compatibility_Test
    rem — Calculation of the optimization region —
    set /a w=!console_width!-1
    set /a h=!console_height!-1
    set /a inner_w=w-2

    rem -- Create a horizontal string (inner_w "─" characters) --
    set "horiz="
    for /L %%i in (1,1,!inner_w!) do set "horiz=!horiz!─"

    rem — display header —
    echo !esc![93m   === Display Compatibility Test (Optimized: !w!×!h!) === !esc![0m
    echo !esc![93m    Check that the entire frame is displayed correctly!esc![0m
    echo.

    rem — Frame drawing: top edge —
    echo !esc![1;1H!esc![93m┌!horiz!┐!esc![0m

    rem — Frame drawing: Only vertical lines inside (no spaces) —
    for /L %%r in (2,1,!h!-1) do (
        echo !esc![%%r;1H!esc![93m│!esc![%%r;!w!H│!esc![0m
        %tools_dir%\cmdwiz.exe delay 10
    )

    rem — Frame drawing: bottom edge —
    echo !esc![!h!;1H!esc![93m└!horiz!┘!esc![0m

    timeout /t 1 >nul
    exit /b 0